plugins {
    id 'java'
}

group mod_org_id
version mod_version

// Still requires java 8 to build, because newer gradle versions require Java 8!
sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

repositories {
    mavenCentral()
}

dependencies {
//    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
//    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'

    implementation files(starmade_root + 'StarMade.jar')
    implementation fileTree(dir: starmade_root + 'lib', include: ["*.jar"])
}

jar {
    //noinspection GroovyAssignabilityCheck
    manifest {
        //noinspection GroovyAssignabilityCheck
        attributes([
                'Built-By': System.getProperty("user.name"),
                'url': mod_url,
                'Implementation-Title': mod_name,
                'Implementation-Version': mod_version,
                'Implementation-Vendor-Id': mod_org_id,
                'Implementation-Vendor': mod_org
        ])
    }
}

task runGame(type: JavaExec) {
    group = 'Game'
    description = 'Run the game with the mod injected for debugging.'
    classpath = sourceSets.main.compileClasspath
    mainClass.set('me.jakev.starloader.LaunchClassLoader')
    systemProperty('java.library.path', starmade_root + "native" + System.getProperty('path.separator') + starmade_root + "native/windows/x64" + System.getProperty('path.separator') + starmade_root + "native/solaris/x64")
    //noinspection GroovyAssignabilityCheck
    args = ["-client", "-force"]
    doFirst {
        //noinspection GroovyAssignabilityCheck
        workingDir = new File(starmade_root)
        File jarFile = jar.archiveFile.get().asFile
        copy {
            from jarFile.getParent()
            into starmade_root + "mods"

            include jarFile.getName()
        }
    }
}

tasks.runGame.dependsOn(tasks.jar)

test {
//    useJUnitPlatform()
}
